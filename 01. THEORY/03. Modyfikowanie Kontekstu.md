# ALL i REMOVEFILTERS - usuwanie filtrów

### Czym jest ALL?

ALL usuwa filtry z tabeli lub kolumny. Mówisz silnikowi DAX: "zignoruj wszelkie filtry, które użytkownik (lub inne miary) nałożył na ten obiekt".

```dax
ALL(tabela)
ALL(tabela[kolumna])
ALL(tabela[kolumna1], tabela[kolumna2], ...)
```

### Podstawowy przypadek - procent od całości

Użytkownik wybiera w slicerze kategorię "Computers". Chcesz pokazać:

- Sprzedaż Computers: 400 000 zł
- Sprzedaż całkowita: 1 000 000 zł (mimo filtru!)
- Udział: 40%

```dax
Total fSprzedaż = SUM(ffSprzedaż[fSprzedażAmount])

Total fSprzedaż All Categories = 
CALCULATE(
    [Total fSprzedaż],
    ALL(dProduct[ProductCategoryName])
)

fSprzedaż % = DIVIDE([Total fSprzedaż], [Total fSprzedaż All Categories])
```

Co się dzieje:

| Kontekst użytkownika | [Total fSprzedaż] | [Total fSprzedaż All Categories] |
| -------------------- | ------------- | ---------------------------- |
| Computers            | 400 000 zł    | 1 000 000 zł                 |
| Cameras              | 250 000 zł    | 1 000 000 zł                 |
| TV                   | 350 000 zł    | 1 000 000 zł                 |

`ALL(dProduct[ProductCategoryName])` mówi: "usuń filtr z kategorii, ale zostaw inne filtry (np. na roku, kraju)".

---
### ALL na całej tabeli vs na kolumnie
- Usuwa filtr **TYLKO** z kategorii
- Filtr na kraju, roku itp. nadal działa
```dax
CALCULATE(
	[Total fSprzedaż],
	ALL(dProduct[ProductCategoryName])
)
```

-  Usuwa **WSZYSTKIE** filtry z tabeli **dProduct**
-  Kolor, marka, podkategoria - wszystko zignorowane

```dax
CALCULATE(
	[Total fSprzedaż],
	 ALL(dProduct)
 )
```

---
### REMOVEFILTERS - nowszy odpowiednik ALL

Od 2019 roku mamy `REMOVEFILTERS`, który robi dokładnie to samo co `ALL` w kontekście `CALCULATE`:

```dax
// Te dwie miary są identyczne:
CALCULATE(
	[Total fSprzedaż],
	 ALL(dProduct)
 )
CALCULATE(
	[Total fSprzedaż],
	 REMOVEFILTERS(dProduct)
)
```

Dlaczego `REMOVEFILTERS` jest lepszy?

1. Nazwa jasno mówi co robi (usuwa filtry, nie "wszystko")
2. `ALL` ma podwójne znaczenie - jako modyfikator filtra i jako funkcja tabelaryczna
3. Czytelność kodu

```dax
// ALL jako funkcja tabelaryczna (zwraca tabelę bez filtrów)
All Products Table = ALL(dProduct)

// ALL jako modyfikator w CALCULATE (usuwa filtry)
fSprzedaż Without Filter = CALCULATE([Total fSprzedaż], ALL(dProduct))

// REMOVEFILTERS - jednoznaczne, tylko modyfikator
fSprzedaż Without Filter = CALCULATE([Total fSprzedaż], REMOVEFILTERS(dProduct))
```


## ALLEXCEPT - usuń wszystko OPRÓCZ

### Problem, który rozwiązuje ALLEXCEPT

Wyobraź sobie tabelę dProduct z kolumnami: ProductKey, ProductName, ProductCategoryName, ProductSubcategoryName, BrandName, Color, UnitPrice.

Chcesz policzyć udział produktu w swojej kategorii. Potrzebujesz usunąć filtry z produktu, ale ZACHOWAĆ filtr kategorii.

```dax
// Podejście 1: wymieniasz wszystko co chcesz usunąć
fSprzedaż Category Total v1 =
CALCULATE(
    [Total fSprzedaż],
    ALL(
        dProduct[ProductKey],
        dProduct[ProductName],
        dProduct[ProductSubcategoryName],
        dProduct[BrandName],
        dProduct[Color],
        dProduct[UnitPrice]
    )
)
```

Działa, ale jest nieczytelne i łatwo o pomyłkę. A co jeśli ktoś doda nową kolumnę do tabeli?

```dax
// Podejście 2: ALLEXCEPT
fSprzedaż Category Total v2 =
CALCULATE(
    [Total fSprzedaż],
    ALLEXCEPT(dProduct, dProduct[ProductCategoryName])
)
```

`ALLEXCEPT` mówi: "usuń wszystkie filtry z dProduct OPRÓCZ filtra na ProductCategoryName".

---
### Wizualizacja działania

Raport pokazuje produkty w kategorii "Computers":

|Produkt|[Total fSprzedaż]|[fSprzedaż Category Total]|Udział|
|---|---|---|---|
|Laptop A|150 000 zł|400 000 zł|37.5%|
|Laptop B|120 000 zł|400 000 zł|30.0%|
|Desktop C|130 000 zł|400 000 zł|32.5%|

Gdy użytkownik przełączy na kategorię "Cameras":

|Produkt|[Total fSprzedaż]|[fSprzedaż Category Total]|Udział|
|---|---|---|---|
|Camera X|100 000 zł|250 000 zł|40.0%|
|Camera Y|150 000 zł|250 000 zł|60.0%|

Filtr kategorii jest respektowany - total zmienia się z 400 000 na 250 000.

---
### ALLEXCEPT z wieloma kolumnami

Możesz zachować filtry na wielu kolumnach:

```dax
// Udział produktu w swojej kategorii I kraju
fSprzedaż Category Country Total =
CALCULATE(
    [Total fSprzedaż],
    ALLEXCEPT(
        dProduct,
        dProduct[ProductCategoryName]
    ),
    ALLEXCEPT(
        dGeography,
        dGeography[RegionCountryName]
    )
)
```


## ALLSELECTED - uwzględnij kontekst fragmentatora

Masz raport z tabelą pokazującą sprzedaż po kategoriach. Użytkownik wybiera w slicerze trzy kategorie: Computers, Cameras, TV. Chce widzieć udział każdej z tych trzech w ich łącznej sumie.

```dax
// Próba z ALL
fSprzedaż % with ALL =
DIVIDE(
    [Total fSprzedaż],
    CALCULATE([Total fSprzedaż], ALL(dProduct[ProductCategoryName]))
)
```

Problem: ALL usuwa WSZYSTKIE filtry z kategorii - czyli ignoruje też slicer. Mianownik to sprzedaż wszystkich kategorii w bazie (także Audio, Games, itd.), nie tylko trzech wybranych.

|Kategoria|[Total fSprzedaż]|Mianownik (ALL)|%|
|---|---|---|---|
|Computers|400 000 zł|2 000 000 zł|20%|
|Cameras|250 000 zł|2 000 000 zł|12.5%|
|TV|350 000 zł|2 000 000 zł|17.5%|
|**Suma**|**1 000 000 zł**||**50%** ← nie sumuje się do 100%!|

---
### ALLSELECTED - rozwiązanie

ALLSELECTED usuwa filtry z wizualizacji (tabeli, wykresu), ale ZACHOWUJE filtry zewnętrzne (slicery, filtry strony/raportu).

```dax
fSprzedaż % with ALLSELECTED =
DIVIDE(
    [Total fSprzedaż],
    CALCULATE([Total fSprzedaż], ALLSELECTED(dProduct[ProductCategoryName]))
)
```

Teraz mianownik to suma tylko dla kategorii wybranych w slicerze:

|Kategoria|[Total fSprzedaż]|Mianownik (ALLSELECTED)|%|
|---|---|---|---|
|Computers|400 000 zł|1 000 000 zł|40%|
|Cameras|250 000 zł|1 000 000 zł|25%|
|TV|350 000 zł|1 000 000 zł|35%|
|**Suma**|**1 000 000 zł**||**100%** ✓|

### ALLSELECTED bez argumentów

Możesz użyć ALLSELECTED() bez podawania tabeli/kolumny - wtedy usuwa filtry wizualizacji ze WSZYSTKICH tabel:

```dax
// Usuwa filtr wizualizacji tylko z kategorii
CALCULATE(
	[Total fSprzedaż],
	 ALLSELECTED(dProduct[ProductCategoryName])
 )
```


```dax
// Usuwa filtr wizualizacji ze wszystkiego (produkty, czas, geografia...)
CALCULATE(
	[Total fSprzedaż],
	 ALLSELECTED()
 )
```

---
### Porównanie ALL vs ALLSELECTED

```dax
Total fSprzedaż = SUM(ffSprzedaż[fSprzedażAmount])

// Ignoruje WSZYSTKO - zawsze pełna baza
Grand Total ALL =
CALCULATE([Total fSprzedaż], ALL(dProduct))

// Ignoruje wizualizację, respektuje slicery
Grand Total ALLSELECTED =
CALCULATE([Total fSprzedaż], ALLSELECTED(dProduct))
```

Scenariusz: Slicer = "Computers", tabela pokazuje podkategorie:

| Podkategoria | [Total fSprzedaż] | [Grand Total ALL] | [Grand Total ALLSELECTED] |
| ------------ | ------------- | ----------------- | ------------------------- |
| Laptops      | 200 000 zł    | 2 000 000 zł      | 400 000 zł                |
| Desktops     | 150 000 zł    | 2 000 000 zł      | 400 000 zł                |
| Tablets      | 50 000 zł     | 2 000 000 zł      | 400 000 zł                |
|              |               |                   |                           |

- ALL daje całą bazę (2 mln)
- ALLSELECTED daje sumę dla "Computers" (400k) - respektuje fragmentator


---
## KEEPFILTERS - zachowaj istniejące filtry

### Problem: CALCULATE nadpisuje filtry

Domyślnie filtr w CALCULATE **zastępuje** istniejący filtr na tej samej kolumnie:

```dax
fSprzedaż Computers =
CALCULATE(
    [Total fSprzedaż],
    dProduct[ProductCategoryName] = "Computers"
)
```

Co się stanie, gdy użytkownik wybierze w slicerze "Cameras"?

| Kontekst użytkownika | [Total fSprzedaż] | [fSprzedaż Computers] |
| -------------------- | ------------- | ----------------- |
| (brak filtra)        | 1 000 000 zł  | 400 000 zł        |
| Cameras              | 250 000 zł    | 400 000 zł        |
| Computers            | 400 000 zł    | 400 000 zł        |

Filtr z CALCULATE wygrywa - nadpisuje wybór użytkownika. Czasem tego chcesz, czasem nie.

---
### KEEPFILTERS - przecięcie filtrów

KEEPFILTERS mówi: "nie nadpisuj, tylko znajdź część wspólną".

```dax
fSprzedaż Computers Keep =
CALCULATE(
    [Total fSprzedaż],
    KEEPFILTERS(dProduct[ProductCategoryName] = "Computers")
)
```

Teraz:

|Kontekst użytkownika|[fSprzedaż Computers]|[fSprzedaż Computers Keep]|
|---|---|---|
|(brak filtra)|400 000 zł|400 000 zł|
|Cameras|400 000 zł|**BLANK**|
|Computers|400 000 zł|400 000 zł|

Gdy użytkownik wybiera "Cameras", KEEPFILTERS szuka przecięcia: (Cameras) ∩ (Computers) = zbiór pusty = BLANK.

---
### Praktyczny przypadek dla KEEPFILTERS

Masz raport z podziałem na segmenty klientów. Chcesz pokazać sprzedaż "Premium" tylko gdy użytkownik patrzy na segment Premium (nie na siłę):

```dax
// Bez KEEPFILTERS - zawsze pokazuje Premium, nawet gdy wybrano "Standard"
fSprzedaż Premium Wrong =
CALCULATE([Total fSprzedaż], dCustomer[Segment] = "Premium")

// Z KEEPFILTERS - pokazuje Premium tylko gdy kontekst to Premium
fSprzedaż Premium Correct =
CALCULATE([Total fSprzedaż], KEEPFILTERS(dCustomer[Segment] = "Premium"))
```

---
### Kiedy używać KEEPFILTERS?

Rzadko - ale są scenariusze:

1. Walidacja danych (pokaż wartość tylko gdy kontekst się zgadza)
2. Warunkowe KPI (np. "cel" ma sens tylko dla konkretnego regionu)
3. Unikanie "wyciekania" danych do innych kontekstów

---
## CROSSFILTER

Umożliwia tymczasową zmianę kierunku filtrowania relacji w kontekście danego wyrażenia DAX.
```dax
CROSSFILTER(<columnName1>, <columnName2>, <direction>)
```

- `Both` — filtrowanie dwukierunkowe (filtry przechodzą w obu kierunkach, domyślnie jest jednokierunkowe).
- `None` — wyłącza filtrowanie między tabelami.
- `OneWay` — jednokierunkowe filtrowanie z Column1 do Column2 (domyślnie).
- `OneWayReverse` — jednokierunkowe filtrowanie z Column2 do Column1.

```dax
Filtruj w obie strony = 
	CALCULATE(
		[Distinct Count of ProductKey],
		 CROSSFILTER(
			FactInternetfSprzedaż[ProductKey],
			DimProduct[ProductKey],
			Both
		)
	)
```

---
## USERELATIONSHIP
**USERELATIONSHIP** aktywuje nieaktywną relację między tabelami na czas trwania obliczenia. W modelu Power BI między dwiema tabelami może istnieć wiele relacji, ale tylko jedna może być aktywna domyślnie.

### Typowy scenariusz
Najczęstszy przypadek to tabela faktów z wieloma kolumnami dat powiązanymi z jedną tabelą kalendarza:
```dax
fSprzedaż
├─ Data Sprzedaży      → 'dKalendarz'[Data] (relacja aktywna)
├─ Data Fakturowania   → 'dKalendarz'[Data] (relacja nieaktywna)
└─ Data Dostawy        → 'dKalendarz'[Data] (relacja nieaktywna)
```

![[Pasted image 20251209193536.png]]
## Podstawowa składnia

```dax
------- Miara używająca domyślnej relacji (OrderDate)
fSprzedaż Amount = 
    SUMX ( fSprzedaż, fSprzedaż[Quantity] * fSprzedaż[Unit Price] )

------- Miara aktywująca relację ShipmentDate
fSprzedaż Amount by Shipment = 
CALCULATE (
    SUMX ( fSprzedaż, fSprzedaż[Quantity] * fSprzedaż[Unit Price] ),
    USERELATIONSHIP ( fSprzedaż[ShipmentDate], 'Calendar'[Date] )
)

------- Miara aktywująca relację DeliveryDate
fSprzedaż Amount by Delivery = 
CALCULATE (
    SUMX ( fSprzedaż, fSprzedaż[Quantity] * fSprzedaż[Unit Price] ),
    USERELATIONSHIP ( fSprzedaż[DeliveryDate], 'Calendar'[Date] )
)
```

**Kluczowe zasady:**
- USERELATIONSHIP działa tylko wewnątrz CALCULATE lub CALCULATETABLE
- Pierwszym parametrem jest kolumna z tabeli faktów (strona "wiele")
- Drugim parametrem jest kolumna z tabeli wymiaru (strona "jeden")
- Relacja musi fizycznie istnieć w modelu (być zdefiniowana, choć nieaktywna)