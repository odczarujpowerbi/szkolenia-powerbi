### Podstawowe użycie: SUMMARIZE vs SUMMARIZECOLUMNS

**Podejście legacy (SUMMARIZE):**

- Wymaga dodatkowego ADDCOLUMNS do obliczenia miar
- Trzeba ręcznie filtrować puste wiersze przez FILTER + NOT ISBLANK
- Bardziej rozwlekłe i podatne na błędy kontekstowe
- SUMMARIZE może dać niespodziewane wyniki bez ADDCOLUMNS

**Podejście modern (SUMMARIZECOLUMNS):**

- Łączy grupowanie, dodawanie obliczeń i usuwanie pustych wierszy w jednej funkcji
- Krótsze, czytelniejsze, bezpieczniejsze
- Obecnie zalecany standard do raportów i zapytań w DAX


```dax
------- LEGACY  
EVALUATE  
    FILTER (  
        ADDCOLUMNS (  
            SUMMARIZE ( Sales, 'Product'[Brand], 'Date'[Year] ),  
            "Sales", CALCULATE ( SUMX ( Sales, Sales[Quantity] * Sales[Unit Price] ) )  
        ),  
        NOT ISBLANK ( [Sales] )  
    )  
      
------- MODERN  
EVALUATE  
    SUMMARIZECOLUMNS (  
        'Product'[Brand], 'Date'[Year],  
        "Sales", CALCULATE ( SUMX ( Sales, Sales[Quantity] * Sales[Unit Price] ) )  
    )
```


---
### Praca z wieloma tabelami faktów

**Zalety SUMMARIZECOLUMNS:**

- Rozumie kontekst całego modelu danych
- Działa spójnie z modelem gwiazdy – automatycznie obsługuje kombinacje wymiarów
- Pozwala w jednym wyrażeniu pobierać dane z różnych tabel faktów
- Zwraca kompletny wynik w jednej tabeli

**Problemy z SUMMARIZE:**

- Ogranicza się do jednej tabeli faktów
- Wymaga ręcznego tworzenia siatki kombinacji przez CROSSJOIN
- Trzeba łączyć wyniki przez NATURALLEFTOUTERJOIN
- Bardziej skomplikowane, mniej czytelne i podatne na błędy

```dax
------- RECOMMENDED  
EVALUATE  
    SUMMARIZECOLUMNS (  
        'Product'[Category],  
        Store[Country],  
        "Sales", CALCULATE ( SUMX ( Sales, Sales[Quantity] * Sales[Net Price] ) ),  
        "Purchases", CALCULATE ( SUMX ( Purchase, Purchase[Quantity] * Purchase[Unit Cost] ) )   
    )  

------- PROBLEMATIC  
EVALUATE  
    VAR S =   
        ADDCOLUMNS (  
            SUMMARIZE ( Sales, 'Product'[Category], Store[Country] ),  
            "Sales", CALCULATE ( SUMX ( Sales, Sales[Quantity] * Sales[Net Price] ) )   
        )  
    VAR P =   
        ADDCOLUMNS (  
            SUMMARIZE ( Purchase, 'Product'[Category], Store[Country] ),  
            "Purchase", CALCULATE ( SUMX ( Purchase, Purchase[Quantity] * Purchase[Unit Cost] ) )   
        )  
    VAR G =   
        CROSSJOIN( VALUES ( 'Product'[Category] ), VALUES ( Store[Country] ) )  
RETURN  
    NATURALLEFTOUTERJOIN ( NATURALLEFTOUTERJOIN ( G, P ), S )
```


---
## Kiedy używać SUMMARIZE zamiast SUMMARIZECOLUMNS

**Gdy potrzebujesz tylko istniejących kombinacji:**

- SUMMARIZE z tabelą źródłową zwraca tylko kombinacje występujące w danych faktycznych
- Filtruje automatycznie do rzeczywistych relacji w tabeli Sales
- To najczęściej oczekiwane zachowanie w raportach

**Problem z SUMMARIZECOLUMNS bez miar:**

- Bez tabeli źródłowej generuje wszystkie możliwe kombinacje (jak CROSSJOIN)
- Może zwrócić wiele pustych kombinacji, które nie istnieją w danych
- Nie jest to zwykle pożądane zachowanie


```dax
------- RECOMMENDED  
EVALUATE  
    SUMMARIZE (  
        'Sales',  
        'Product'[Brand],  
        Customer[Continent],  
        'Date'[Date]  
    )  

------- PROBLEMATIC  
EVALUATE  
    SUMMARIZECOLUMNS (  
        'Product'[Brand],  
        Customer[Continent],  
        'Date'[Date]  
    )  

-- Równoważne z:  
EVALUATE  
    CROSSJOIN (  
        VALUES ( 'Product'[Brand] ),  
        VALUES ( Customer[Continent] ),  
        VALUES ( 'Date'[Date] )   
    )
```

**Zasada:** Do samego grupowania bez miar używaj SUMMARIZE z tabelą źródłową. Do grupowania z miarami używaj SUMMARIZECOLUMNS.